plugins {
    id 'java'
    id 'net.neoforged.moddev.repositories' version '1.0.14'
    id 'maven-publish'
}

group = 'de.vfyjxf'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
    }
    maven {
        name = 'neoforge'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {

    compileOnly "org.jetbrains:annotations:24.1.0"
    compileOnly "cpw.mods:securejarhandler:3.0.8"
    compileOnly "cpw.mods:bootstraplauncher:2.0.2"
    compileOnly "net.neoforged.fancymodloader:loader:4.0.41"
    compileOnly "net.fabricmc:sponge-mixin:0.16.5+mixin.0.8.7"
}

java {
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }

}

jar {
    manifest {
        attributes(
                "Premain-Class": "dev.vfyjxf.agent.AgentEntryPoint",
                "Agent-Class": "dev.vfyjxf.agent.AgentEntryPoint",
                "Can-Redefine-Classes": "true",
                "Can-Retransform-Classes": "true"
        )
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
            group = 'dev.vfyjxf.agent'
            version = project.version
            artifactId = 'mixin-hotswap-agent'

        }
    }
    repositories {
        mavenLocal()
    }
}